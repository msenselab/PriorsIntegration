//   
// This Stan program defines a local-global model(LGM) to fit the IR condition data 
// to estimate the glboal prior.
// local-global model(LGM) assumes that the sensory input(DS) first integrates 
// with the local prior(PL) to form posterior(DL), and the formed posterior(DL) 
// further integrates with the global prior(PG) to generate reproduction (Dr)
// step1: integration of sensory input with local priors; 
// step2: posterior variances D_L; 
// step3: integration of posterior with global prior


// The user defined functions
functions {
  //covert lognormal distribution mu to normal distribution mu
  real mu_normal(real mu, real variance){
    return(exp(mu+ variance *0.5));
  }
  //covert lognormal distribution variance to normal distribution variance
  real variance_normal(real mu, real variance){
    return((exp(variance)-1)*exp(2*mu + variance));
  }
  
  real get_weight(real sig_t, real sig_p){
    return(sig_t /(sig_t + sig_p));
  }
  
  real get_mu(real wp_p, real mu_p, real x){  
    return(wp_p *mu_p +(1-wp_p) * x);
  }
  
  real get_variance(real sig_t, real sig_p){
    return(sig_t*sig_p/(sig_t + sig_p));
  }
  
  matrix predictor_mix_rng(real[] x, real sig2_t, real sig_pr2_s_log, real sig_pr2_l_log, real sig2_p_g_log, real mu_p_s_log, real mu_p_l_log, real mu_p_g_log, real sig2_mn) {
    vector[num_elements(x)] predY;         //predication of RP generated by model
    vector[num_elements(x)] wp_local;
    vector[num_elements(x)] mu_dL;
    vector[num_elements(x)] sig_dL_sq;
    vector[num_elements(x)] wp;
    vector[num_elements(x)] mu_r_log;
    vector[num_elements(x)] mu_r;
    vector[num_elements(x)] sig_r_log;
    vector[num_elements(x)] sig_r;
    vector[num_elements(x)] log_lik;
    
    for (m in 1:num_elements(x)) {
      //part 1 integration of local priors firstly
      if (x[m] < 1){
        wp_local[m] = get_weight(sig2_t, sig_pr2_s_log); //weight of local short prior
        mu_dL[m] = get_mu(wp_local[m], mu_p_s_log, log(x[m]));
        sig_dL_sq[m] = get_variance(sig2_t, sig_pr2_s_log);
      }else{
        wp_local[m] = get_weight(sig2_t, sig_pr2_l_log); //weight of local long prior
        mu_dL[m] = get_mu(wp_local[m], mu_p_l_log, log(x[m]));
        sig_dL_sq[m] = get_variance(sig2_t, sig_pr2_l_log);
      }
      
      //part3 integration of the global prior
      wp[m] = get_weight(sig_dL_sq[m], sig2_p_g_log);  // weight of global prior in De
      mu_r_log[m] =  get_mu(wp[m], mu_p_g_log, mu_dL[m]);
      sig_r_log[m] = get_variance(sig2_p_g_log, sig_dL_sq[m]);  
      
      mu_r[m] = mu_normal(mu_r_log[m], sig_r_log[m]);
      sig_r[m] =sqrt(variance_normal(mu_r_log[m], sig_r_log[m]) +sig2_mn);
      predY[m] = normal_rng(mu_r[m], sig_r[m]); 
      log_lik[m] = normal_lpdf(predY[m]|mu_r[m], sig_r[m]);
    }
    return(append_col(append_col(append_col(wp, mu_r), append_col(sig_r, predY)), append_col(append_col(wp_local, sig_dL_sq), log_lik)));
  }
}

// The input data list
data {
  int<lower=0> n_mix;  //number of data points in the mix session 
  real<lower=0> X_mix[n_mix];   //stimulus duration (mix group)
  real<lower=0> Y_mix[n_mix];   //measured reproductive duration (mixed) 
  real<lower=0> xnew[162];  //new target duration for mixed group
  real mu_p_s_log;   // mean of short prior of short session in log scale
  real mu_p_l_log;   // mean of long prior of long session in log scale
  real sig_pr2_s_log;  // variance of short prior of short session in log scale
  real sig_pr2_l_log; // variance of long prior of long session in log scale
  real<lower=0, upper=4> sig2_mn;
  real<lower=0, upper=4> sig2_t; 
}

// The parameters accepted by the model. Our model
// accepts two parameters 'mu_p_g' and 'sig2_p_g'.
parameters {
  //hyperparameters
  real<lower=0,upper=2.5> mu_p_g;   // mean of global prior in linear scale
  real<lower=0, upper=4> sig2_p_g;  //variance of global prior in linear scale
}


transformed parameters { 
  real mu_p_g_log;  // mean of global prior in log scale
  real<lower=0> sig2_p_g_log;  //variance of global prior in log scale
  mu_p_g_log = log(mu_p_g) - 0.5 *log(1+sig2_p_g/mu_p_g^2);
  sig2_p_g_log = log(1+sig2_p_g/mu_p_g^2);
} 

// The model to be estimated
model {
  vector[n_mix] wp_local;
  vector[n_mix] sig_dL_sq;
  vector[n_mix] mu_dL;
  vector[n_mix] wp;
  vector[n_mix] mu_r_log;
  vector[n_mix] sig_r_log;
  vector[n_mix] mu_r;
  vector[n_mix] sig_r;
  
  //mixed session
  for (m in 1:n_mix) {
    //integration of local priors firstly
    if (X_mix[m] < 1){
      wp_local[m] = get_weight(sig2_t, sig_pr2_s_log); //weight of local short prior
      mu_dL[m] = get_mu(wp_local[m], mu_p_s_log, log(X_mix[m]));
      sig_dL_sq[m] =  get_variance(sig2_t, sig_pr2_s_log);
    }else{
      wp_local[m] = get_weight(sig2_t, sig_pr2_l_log); //weight of local long prior
      mu_dL[m] = get_mu(wp_local[m], mu_p_l_log, log(X_mix[m]));
      sig_dL_sq[m] = get_variance(sig2_t, sig_pr2_l_log);
    }
    
    //integration of the global prior
    wp[m] = get_weight(sig_dL_sq[m], sig2_p_g_log);
    mu_r_log[m] = get_mu(wp[m], mu_p_g_log, mu_dL[m]);
    sig_r_log[m] = get_variance(sig2_p_g_log, sig_dL_sq[m]);
    mu_r[m] = mu_normal(mu_r_log[m], sig_r_log[m]);
    sig_r[m] = sqrt(variance_normal(mu_r_log[m], sig_r_log[m]) +sig2_mn);
    Y_mix[m] ~ normal(mu_r[m], sig_r[m]);  
  }
}

// predicted data based on the estimated parameters
generated quantities {
  matrix[n_mix, 7] ypred_mix;
  matrix[162, 7] ynew_mix;
  vector[n_mix] log_lik;
  real log_lik_sum;
  ynew_mix = predictor_mix_rng(xnew, sig2_t, sig_pr2_s_log, sig_pr2_l_log, sig2_p_g_log, mu_p_s_log, mu_p_l_log, mu_p_g_log, sig2_mn);
  ypred_mix = predictor_mix_rng(X_mix, sig2_t, sig_pr2_s_log, sig_pr2_l_log, sig2_p_g_log, mu_p_s_log, mu_p_l_log, mu_p_g_log, sig2_mn);
  log_lik = col(ypred_mix, 7);
  log_lik_sum = sum(log_lik);
}